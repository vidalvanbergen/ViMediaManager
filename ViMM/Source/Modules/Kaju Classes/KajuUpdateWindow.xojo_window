#tag Window
Begin Window KajuUpdateWindow
   BackColor       =   &cFFFFFF00
   Backdrop        =   0
   CloseButton     =   False
   Compatibility   =   ""
   Composite       =   True
   Frame           =   0
   FullScreen      =   False
   FullScreenButton=   False
   HasBackColor    =   False
   Height          =   600
   ImplicitInstance=   True
   LiveResize      =   False
   MacProcID       =   0
   MaxHeight       =   32000
   MaximizeButton  =   False
   MaxWidth        =   32000
   MenuBar         =   0
   MenuBarVisible  =   True
   MinHeight       =   64
   MinimizeButton  =   False
   MinWidth        =   64
   Placement       =   2
   Resizeable      =   False
   Title           =   "#KajuLocale.kWindowTitle"
   Visible         =   True
   Width           =   800
   Begin HTMLViewer hvNotes
      AutoDeactivate  =   True
      Enabled         =   True
      Height          =   445
      HelpTag         =   ""
      Index           =   -2147483648
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Renderer        =   1
      Scope           =   2
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Top             =   84
      Visible         =   True
      Width           =   631
   End
   Begin PushButton btnOK
      AutoDeactivate  =   True
      Bold            =   False
      ButtonStyle     =   "0"
      Cancel          =   False
      Caption         =   "#KajuLocale.kInstallButton"
      Default         =   True
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   630
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin PushButton btnCancel
      AutoDeactivate  =   True
      Bold            =   False
      ButtonStyle     =   "0"
      Cancel          =   True
      Caption         =   "#KajuLocale.kRemindMeLaterButton"
      Default         =   False
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   468
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin PushButton btnSkipVersion
      AutoDeactivate  =   True
      Bold            =   False
      ButtonStyle     =   "0"
      Cancel          =   False
      Caption         =   "#KajuLocale.kSkipVersionButton"
      Default         =   False
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin Label lblMain
      AutoDeactivate  =   True
      Bold            =   True
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "Untitled"
      TextAlign       =   0
      TextColor       =   &c00000000
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   14
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin Label lblSecondary
      AutoDeactivate  =   True
      Bold            =   False
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   25
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   True
      Scope           =   2
      Selectable      =   False
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "Untitled"
      TextAlign       =   0
      TextColor       =   &c00000000
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   39
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin Label Label1
      AutoDeactivate  =   True
      Bold            =   True
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kReleaseNotesLabel"
      TextAlign       =   0
      TextColor       =   &c00000000
      TextFont        =   "SmallSystem"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   64
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin Label lblInstallMessage
      AutoDeactivate  =   True
      Bold            =   False
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   30
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   True
      Scope           =   2
      Selectable      =   False
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kDownloadingMessage"
      TextAlign       =   0
      TextColor       =   &c00000000
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   555
      Transparent     =   True
      Underline       =   False
      Visible         =   False
      Width           =   294
   End
   Begin ProgressBar pbProgress
      AutoDeactivate  =   True
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   20
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Maximum         =   100
      Scope           =   2
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Top             =   555
      Transparent     =   False
      Value           =   0
      Visible         =   False
      Width           =   117
   End
   Begin Kaju.ZipShell shZipper
      Arguments       =   ""
      Backend         =   ""
      Canonical       =   False
      Enabled         =   True
      ErrorCode       =   0
      Index           =   -2147483648
      InitialParent   =   ""
      IsRunning       =   False
      LockedInPosition=   False
      Mode            =   1
      PID             =   0
      Result          =   ""
      Scope           =   2
      TabPanelIndex   =   0
      TimeOut         =   0
   End
   Begin HTMLViewer hvNewWindow
      AutoDeactivate  =   True
      Enabled         =   True
      Height          =   200
      HelpTag         =   ""
      Index           =   -2147483648
      Left            =   -345
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Renderer        =   0
      Scope           =   2
      TabIndex        =   9
      TabPanelIndex   =   0
      TabStop         =   True
      Top             =   119
      Visible         =   False
      Width           =   300
   End
   Begin Label lblVersions
      AutoDeactivate  =   True
      Bold            =   True
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   20
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   10
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kVersionsLabel"
      TextAlign       =   0
      TextColor       =   &c00000000
      TextFont        =   "SmallSystem"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   64
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   117
   End
   Begin PopupMenu pumUpdates
      AutoDeactivate  =   True
      Bold            =   False
      DataField       =   ""
      DataSource      =   ""
      Enabled         =   True
      Height          =   20
      HelpTag         =   ""
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   36
      ListIndex       =   0
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      TabIndex        =   11
      TabPanelIndex   =   0
      TabStop         =   True
      TextFont        =   "System"
      TextSize        =   0.0
      TextUnit        =   0
      Top             =   87
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   101
   End
   Begin Timer tmrTimeout
      Enabled         =   True
      Index           =   -2147483648
      InitialParent   =   ""
      LockedInPosition=   False
      Mode            =   0
      Period          =   5000
      Scope           =   2
      TabPanelIndex   =   0
   End
   Begin Kaju.HTTPSocketAsync hsSocket
      Enabled         =   True
      Index           =   -2147483648
      LockedInPosition=   False
      Scope           =   2
      TabPanelIndex   =   0
      ValidateCertificates=   False
   End
End
#tag EndWindow

#tag WindowCode
	#tag Event
		Sub Close()
		  if CurrentStage = Stage.Cancelled then
		    for each f as FolderItem in DeleteOnCancel
		      Kaju.DeleteRecursive( f )
		    next
		  end if
		  
		  for each f as FolderItem in DeleteOnClose
		    Kaju.DeleteRecursive( f )
		  next
		  
		  CurrentUpdate = nil
		  
		End Sub
	#tag EndEvent

	#tag Event
		Sub Open()
		  RelativeToFolderItem = GetTemporaryFolderItem
		  DeleteOnClose.Append RelativeToFolderItem
		  
		  #if not TargetMacOS then
		    //
		    // Switch the buttons around for other platforms
		    //
		    dim farLeft as integer = btnCancel.Left
		    btnCancel.Left = btnOK.Left
		    btnOK.Left = farLeft
		    
		    const kAddition = 10
		    
		    btnCancel.Height = btnCancel.Height + kAddition
		    btnOK.Height = btnOK.Height + kAddition
		    btnSkipVersion.Height = btnSkipVersion.Height + kAddition
		    
		    //
		    // Make the pop-up menu bigger
		    //
		    pumUpdates.Height = pumUpdates.Height + kAddition
		    
		    self.Height = self.Height + kAddition
		    
		  #endif
		  
		End Sub
	#tag EndEvent

	#tag Event
		Sub Paint(g As Graphics, areas() As REALbasic.Rect)
		  //
		  // Draw a border around the release notes (Mac only)
		  //
		  
		  const kThickness = 1
		  
		  g.DrawPicture BackgroundImage, 0, 0, g.Width, g.Height
		  
		  dim drawLeft as integer = hvNotes.Left - kThickness
		  dim drawTop as integer = hvNotes.Top - kThickness
		  dim drawRight as integer = hvNotes.Left + hvNotes.Width
		  dim drawBottom as integer = hvNotes.Top + hvNotes.Height
		  dim drawWidth as integer = drawRight - drawLeft + kThickness
		  dim drawHeight as integer = drawBottom - drawTop + kThickness
		  
		  g.PenHeight = kThickness
		  g.PenWidth = kThickness
		  
		  g.ForeColor = &c00000000 // Black
		  g.DrawRect drawLeft, drawTop, drawWidth, drawHeight
		  
		  #if RBVersion > 2012.02 then
		    #pragma unused areas
		  #endif
		End Sub
	#tag EndEvent


	#tag Method, Flags = &h0
		Sub Cancel()
		  CurrentStage = Stage.Cancelled
		  
		  SelectedUpdate = nil
		  if Initiater <> nil then
		    Initiater.Cancel
		    Initiater = nil
		  end if
		  Kaju.CancelUpdate
		  
		  hsSocket.Disconnect
		  
		  if shZipper.IsRunning then
		    shZipper.Close
		  end if
		  
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  if Checker.QuitOnCancelIfRequired and not Checker.DryRun and Checker.Result = Kaju.UpdateChecker.ResultType.RequiredUpdateAvailable then
		    quit
		  else
		    self.Close
		  end if
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ChangeBackgroudImage(update As Kaju.UpdateInformation)
		  dim useTransparency as boolean = update.UseTransparency
		  dim p as Picture = update.Image
		  if p is nil then
		    p = Checker.DefaultImage
		    useTransparency = Checker.DefaultUseTransparency
		  end if
		  
		  if p <> nil and useTransparency then
		    dim faded as new Picture( p.Width, p.Height )
		    
		    const kTransparencyPercent = 50.0
		    faded.Graphics.Transparency = kTransparencyPercent
		    
		    faded.Graphics.DrawPicture( p, 0, 0 )
		    
		    p = faded
		  end if
		  
		  self.BackgroundImage = p
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub ChooseUpdate(checker As Kaju.UpdateChecker, updates() As Kaju.UpdateInformation)
		  self.Checker = checker
		  CurrentStage = Stage.ChoosingUpdate
		  
		  //
		  // Sort the updates
		  //
		  dim vers() as double
		  for i as integer = 0 to updates.Ubound
		    vers.Append updates( i ).VersionAsDouble
		  next
		  
		  vers.SortWith( updates )
		  
		  //
		  // Reverse the sort
		  //
		  dim reverse() as integer
		  for i as integer = 0 to updates.Ubound
		    reverse.Append( updates.Ubound - i )
		  next
		  
		  reverse.SortWith( updates )
		  
		  //
		  // Set up the menu with the available updates.
		  // It will set up the rest of the controls.
		  //
		  
		  for i as integer = 0 to Updates.Ubound
		    dim update as Kaju.UpdateInformation = updates( i )
		    pumUpdates.AddRow update.Version
		    pumUpdates.RowTag( i ) = update
		  next
		  
		  pumUpdates.ListIndex = 0
		  
		  if updates.Ubound = 0 then
		    //
		    // Only one update so hide the menu
		    //
		    lblVersions.Visible = false
		    pumUpdates.Visible = false
		  end if
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CurrentUpdate_ImageReceived(sender As Kaju.UpdateInformation)
		  if sender is CurrentUpdate then
		    ChangeBackgroudImage( CurrentUpdate )
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CurrentUpdate_ReleaseNotesReceived(sender As Kaju.UpdateInformation)
		  if sender is CurrentUpdate then
		    DisplayVersionInfo( CurrentUpdate )
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub DisplayVersionInfo(update As Kaju.UpdateInformation)
		  //
		  // Fill in the viewer
		  //
		  
		  self.Loading = true
		  
		  //
		  // Set up the main labels
		  //
		  AppName = update.AppName
		  if pumUpdates.ListCount = 1 then
		    lblSecondary.Text = KajuLocale.kSecondaryNoticeOne
		    lblSecondary.Text = lblSecondary.Text.ReplaceAll( KajuLocale.kNewVersionMarker, update.Version )
		  else
		    lblSecondary.Text = KajuLocale.kSecondaryNoticeMultiple
		  end if
		  
		  lblMain.Text = KajuLocale.kMainNotice
		  lblMain.Text = lblMain.Text.ReplaceAll( KajuLocale.kAppMarker, AppName )
		  lblSecondary.Text = lblSecondary.Text.ReplaceAll( KajuLocale.kThisVersionMarker, Kaju.AppVersionString )
		  
		  //
		  // Get the background picture, if any
		  //
		  ChangeBackgroudImage update
		  
		  //
		  // Show the release notes
		  //
		  dim source as string = update.DisplayReleaseNotes
		  hvNotes.LoadPage( source, RelativeToFolderItem )
		  
		  //
		  // hvNotes.CancelLoad will set self.Loading back to false
		  //
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub HandleOKButton()
		  select case CurrentStage
		  case Stage.ChoosingUpdate
		    //
		    // The update has been chosen
		    //
		    
		    SelectedUpdate = nil
		    SelectedBinary = nil
		    
		    if true then // Scope
		      dim chosen as Kaju.UpdateInformation = pumUpdates.RowTag( pumUpdates.ListIndex )
		      dim binary as Kaju.BinaryInformation = UserSelectsBinary( chosen )
		      if binary is nil then
		        return
		      end if
		      
		      if not UserConfirmsRequiredPayment( chosen ) then
		        return
		      end if
		      
		      SelectedUpdate = chosen
		      SelectedBinary = binary
		    end if
		    
		    btnOK.Enabled = false
		    
		    btnCancel.Caption = KajuLocale.kStopButton
		    
		    btnSkipVersion.Visible = false
		    pbProgress.Visible = true
		    lblInstallMessage.Visible = true
		    
		    pumUpdates.Enabled = false
		    
		    CurrentStage = Stage.InstallingUpdate
		    
		    if Checker.DryRun then
		      
		      lblInstallMessage.Text = KajuLocale.kDryRunMessage
		      
		    else
		      
		      lblInstallMessage.Text = KajuLocale.kDownloadingMessage
		      
		      dim tempFolder as FolderItem = Kaju.GetTemporaryFolder
		      DeleteOnCancel.Append tempFolder
		      
		      DownloadFile = tempFolder.Child( SelectedBinary.FileName )
		      DeleteOnClose.Append DownloadFile
		      
		      dim url as string = SelectedBinary.URL
		      
		      //
		      // Start the download
		      //
		      hsSocket.Get( url, DownloadFile )
		      
		      //
		      // Start the timeout timer
		      //
		      tmrTimeout.Reset
		      tmrTimeout.Mode = Timer.ModeSingle
		      
		    end if
		    
		  case Stage.WaitingToQuit
		    //
		    // The user chose Quit & Install
		    //
		    
		    Kaju.StartUpdate( self.Initiater )
		    
		    //
		    // Move this window to the back
		    //
		    dim lastWindowIndex as integer = WindowCount - 1
		    if not( Window( lastWindowIndex ) Is self ) then
		      dim showIndex as integer = lastWindowIndex
		      for windowIndex as integer = lastWindowIndex downto 0
		        dim w as Window = Window( showIndex )
		        if w Is self then
		          showIndex = showIndex - 1
		        else
		          w.Show
		        end if
		      next
		    end if
		    
		    Quit
		    
		  end
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub Show()
		  // Override super's show
		  
		  super.Show
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowError(msg As String = "")
		  if msg.Trim = "" then
		    msg = KajuLocale.kGenericErrorMessage
		  end if
		  
		  lblInstallMessage.Visible = true
		  lblInstallMessage.Text = msg
		  pbProgress.Visible = false
		  
		  btnOK.Enabled = false
		  btnCancel.Caption = KajuLocale.kTryLaterButton
		  btnSkipVersion.Visible = false
		  
		  CurrentStage = Stage.UpdateError
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowModal()
		  // Override super's ShowModal
		  
		  // Do nothing
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowModalWithin(parentWindow As Window)
		  // Ovverride super's ShowModalWithin
		  
		  // Do nothing
		  
		  #pragma unused parentWindow
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function UserConfirmsRequiredPayment(update As Kaju.UpdateInformation) As Boolean
		  if update.RequiresPayment then
		    dim dlg as new MessageDialog
		    dlg.ActionButton.Visible = true
		    dlg.ActionButton.Caption = KajuLocale.kProceedButton
		    dlg.CancelButton.Visible = true
		    dlg.Message = KajuLocale.kPaymentRequiredMessage
		    dim btn as MessageDialogButton = dlg.ShowModalWithin( self )
		    
		    if btn is dlg.CancelButton then
		      return false
		    end if
		  end if
		  
		  return true
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function UserSelectsBinary(update As Kaju.UpdateInformation) As Kaju.BinaryInformation
		  //
		  // Make sure we pick the right binary from the update
		  //
		  
		  if TargetMacOS or Target64Bit or not Checker.Allow32bitTo64bitUpdates then
		    return update.PlatformBinarySameBitness
		  elseif update.PlatformBinary64bit is nil then
		    return update.PlatformBinary32bit
		  else
		    //
		    // The current is 32-bit and either both binaries exist or only the 64-bit exists
		    // Let the user choose
		    //
		    
		    dim b32 as Kaju.BinaryInformation = update.PlatformBinary32bit
		    dim b64 as Kaju.BinaryInformation = update.PlatformBinary64bit
		    
		    dim msg as string 
		    if b32 is nil then
		      msg = KajuLocale.kChoose64bitMessage
		    else
		      msg = KajuLocale.kChooseBetweenBitsMessage
		    end if
		    msg = KajuLocale.kCurrenlyUsing32bitMessage + " " + msg
		    
		    dim dlg as new MessageDialog
		    dlg.Message = msg
		    dlg.Explanation = KajuLocale.kExplain64bitMessage
		    if b32 is nil then
		      dlg.ActionButton.Caption = KajuLocale.kProceedButton
		      dlg.ActionButton.Visible = true
		      dlg.AlternateActionButton.Visible = false
		    else
		      dlg.ActionButton.Caption = KajuLocale.kUse32bitLabel
		      dlg.ActionButton.Visible = true
		      dlg.AlternateActionButton.Caption = KajuLocale.kUse64bitLabel
		      dlg.AlternateActionButton.Visible = true
		    end if
		    dlg.CancelButton.Caption = KajuLocale.kCancelButton
		    dlg.CancelButton.Visible = true
		    
		    dim btn as MessageDialogButton = dlg.ShowModalWithin( self )
		    if btn is nil or btn is dlg.CancelButton then
		      return nil
		    elseif b32 is nil or btn is dlg.AlternateActionButton then
		      return b64
		    else
		      return b32
		    end if
		  end if
		End Function
	#tag EndMethod


	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  if mAppName = "" then
			    mAppName = KajuLocale.kThisApplication
			  end if
			  
			  return mAppName
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  mAppName = value
			End Set
		#tag EndSetter
		AppName As String
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  return mBackgroundImage
			  
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  mBackgroundImage = value
			  self.Invalidate
			End Set
		#tag EndSetter
		Private BackgroundImage As Picture
	#tag EndComputedProperty

	#tag Property, Flags = &h21
		Private Checker As Kaju.UpdateChecker
	#tag EndProperty

	#tag Property, Flags = &h21
		Private CurrentStage As Stage
	#tag EndProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  return mCurrentUpdate
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  dim current as Kaju.UpdateInformation = mCurrentUpdate
			  if current isa object then
			    RemoveHandler current.ImageReceived, WeakAddressOf CurrentUpdate_ImageReceived
			    RemoveHandler current.ReleaseNotesReceived, WeakAddressOf CurrentUpdate_ReleaseNotesReceived
			  end if
			  
			  mCurrentUpdate = value
			  if value isa object then
			    AddHandler value.ImageReceived, WeakAddressOf CurrentUpdate_ImageReceived
			    AddHandler value.ReleaseNotesReceived, WeakAddressOf CurrentUpdate_ReleaseNotesReceived
			  end if
			  
			End Set
		#tag EndSetter
		Private CurrentUpdate As Kaju.UpdateInformation
	#tag EndComputedProperty

	#tag Property, Flags = &h21
		#tag Note
			Files/folders that shoudl be deleted if the user cancelled
		#tag EndNote
		Private DeleteOnCancel() As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		#tag Note
			Files/Folders that should be deleted upon window close
		#tag EndNote
		Private DeleteOnClose() As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private DownloadFile As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private Initiater As Kaju.UpdateInitiater
	#tag EndProperty

	#tag Property, Flags = &h21
		Private Loading As Boolean
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mAppName As String
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mBackgroundImage As Picture
	#tag EndProperty

	#tag Property, Flags = &h21
		Attributes( hidden ) Private mCurrentUpdate As Kaju.UpdateInformation
	#tag EndProperty

	#tag Property, Flags = &h21
		Private RelativeToFolderItem As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private SelectedBinary As Kaju.BinaryInformation
	#tag EndProperty

	#tag Property, Flags = &h0
		SelectedUpdate As Kaju.UpdateInformation
	#tag EndProperty


	#tag Enum, Name = Stage, Type = Integer, Flags = &h21
		ChoosingUpdate
		  InstallingUpdate
		  WaitingToQuit
		  UpdateError
		Cancelled
	#tag EndEnum


#tag EndWindowCode

#tag Events hvNotes
	#tag Event
		Function NewWindow() As HTMLViewer
		  return hvNewWindow
		End Function
	#tag EndEvent
	#tag Event
		Function CancelLoad(URL as String) As Boolean
		  #pragma unused URL
		  
		  dim r as boolean = not Loading
		  Loading = false
		  return r
		  
		End Function
	#tag EndEvent
	#tag Event
		Sub Error(errorNumber as Integer, errorMessage as String)
		  #pragma unused errorMessage
		  
		  #if TargetMacOS then
		    const kCancelledCode as integer = -999
		  #else
		    const kCancelledCode as integer = -9999999999
		  #endif
		  
		  if errorNumber <> kCancelledCode then
		    break
		  end if
		  
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnOK
	#tag Event
		Sub Action()
		  HandleOKButton
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnCancel
	#tag Event
		Sub Action()
		  self.Cancel
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnSkipVersion
	#tag Event
		Sub Action()
		  //
		  // We can only ignore versions if we already have the minimum requried
		  //
		  
		  dim info as Kaju.UpdateInformation = pumUpdates.RowTag( pumUpdates.ListIndex )
		  
		  if info.MinimumRequiredVersion <> "" and _
		    Kaju.VersionToDouble( Kaju.AppVersionString ) < Kaju.VersionToDouble( info.MinimumRequiredVersion ) then
		    dim msg as string = KajuLocale.kCannotSkipVersionsMessage
		    msg = msg.ReplaceAll( KajuLocale.kVersionMarker, info.MinimumRequiredVersion )
		    MsgBox msg
		    
		  else
		    
		    Checker.IgnoreVersion( info.Version )
		    
		    if pumUpdates.ListCount = 1 then
		      SelectedUpdate = nil
		      Kaju.CancelUpdate
		      self.Close
		    else
		      pumUpdates.RemoveRow( pumUpdates.ListIndex )
		      pumUpdates.ListIndex = 0
		    end if
		    
		  end if
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events shZipper
	#tag Event
		Sub DecompressCompleted(zipFile As FolderItem, containingFolder As FolderItem)
		  //
		  // No matter what, we don't need the zip file anymore
		  //
		  zipFile.Delete
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  dim cnt as integer = containingFolder.Count
		  
		  if cnt = 0 or SelectedUpdate is nil then
		    
		    ShowError()
		    
		  else
		    
		    //
		    // Delete .DS_Store files
		    //
		    #if not TargetMacOS then
		      Kaju.DeleteDSStoreRecursive( containingFolder )
		    #endif
		    
		    //
		    // Find the executable
		    //
		    dim item as FolderItem
		    for i as integer = 1 to containingFolder.Count
		      dim f as FolderItem = containingFolder.Item( i )
		      dim name as string = f.Name
		      dim leftChars as string = name.Left( 1 )
		      if leftChars <> "." and leftChars <> "_" then
		        item = f
		        exit
		      end if
		    next
		    
		    if item is nil then
		      
		      ShowError()
		      
		    else
		      
		      Initiater = new Kaju.UpdateInitiater
		      Initiater.ReplacementAppFolder = item
		      Initiater.ReplacementExecutableName = SelectedBinary.ExecutableName
		      
		      btnOK.Enabled = true
		      btnOK.Caption = KajuLocale.kQuitButton
		      btnCancel.Visible = true
		      btnCancel.Caption = KajuLocale.kCancelButton
		      
		      pbProgress.Visible = false
		      
		      lblInstallMessage.Visible = true
		      lblInstallMessage.Text = KajuLocale.kReadyMessage
		      
		      CurrentStage = Stage.WaitingToQuit
		      
		    end if
		    
		  end if
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub Error()
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  ShowError()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events hvNewWindow
	#tag Event
		Function CancelLoad(URL as String) As Boolean
		  ShowURL( URL )
		  return true
		  
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events pumUpdates
	#tag Event
		Sub Change()
		  //
		  // Fill in the viewer
		  //
		  
		  CurrentUpdate = nil
		  
		  if me.ListIndex = -1 then
		    if me.ListCount <> 0 then
		      me.ListIndex = 0
		    end if
		    return
		  end if
		  
		  dim update as Kaju.UpdateInformation = me.RowTag( me.ListIndex )
		  DisplayVersionInfo( update )
		  CurrentUpdate = update
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events tmrTimeout
	#tag Event
		Sub Action()
		  hsSocket.Disconnect
		  ShowError( KajuLocale.kTimedOutMessage )
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events hsSocket
	#tag Event
		Sub ReceiveProgress(BytesReceived as Int64, TotalBytes as Int64, NewData as xojo.Core.MemoryBlock)
		  #pragma unused newData
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  //
		  // Have to get the value below 65536
		  //
		  
		  const kMaxAllowed = 1000
		  
		  if totalBytes > kMaxAllowed then
		    dim mult as integer = totalBytes \ kMaxAllowed
		    totalBytes = totalBytes \ mult
		    bytesReceived = bytesReceived \ mult
		  end if
		  
		  pbProgress.Maximum = totalBytes
		  pbProgress.Value = bytesReceived
		  
		  tmrTimeout.Reset
		  
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub Error(err as RuntimeException)
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  dim errMsg as string = err.Message
		  if errMsg = "" then
		    errMsg = KajuLocale.kGenericErrorMessage
		  end if
		  
		  ShowError( errMsg + "  (" + str( err.ErrorNumber ) + ")" )
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub FileReceived(url As String, httpStatus As Integer, file As FolderItem)
		  #pragma unused url
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  pbProgress.Maximum = -1
		  pbProgress.Value = 0
		  
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  if httpStatus <> 200 then
		    
		    ShowError()
		    
		  elseif Kaju.HashOfFile( file ) <> SelectedBinary.Hash then
		    
		    ShowError( KajuLocale.kBadDownloadMessage )
		    
		  else
		    //
		    // We have the file and it appears to be good
		    //
		    
		    lblInstallMessage.Text = KajuLocale.kProcessingFileMessage
		    
		    dim targetFolder as FolderItem
		    #if TargetWindows then
		      dim targetFolderName as string = SelectedUpdate.AppName + "- decompressed"
		      targetFolder = App.ExecutableFile.Parent
		      targetFolder = targetFolder.Child( targetFolderName )
		      Kaju.DeleteRecursive( targetFolder )
		    #else
		      targetFolder = file.Parent.Child( "decompressed" )
		    #endif
		    DeleteOnCancel.Append targetFolder
		    shZipper.Decompress( file, targetFolder )
		  end if
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="AppName"
		Group="Behavior"
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackColor"
		Visible=true
		Group="Appearance"
		InitialValue="&hFFFFFF"
		Type="Color"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Appearance"
		Type="Picture"
		EditorType="Picture"
	#tag EndViewProperty
	#tag ViewProperty
		Name="CloseButton"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composite"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Frame"
		Visible=true
		Group="Appearance"
		InitialValue="0"
		Type="Integer"
		EditorType="Enum"
		#tag EnumValues
			"0 - Document"
			"1 - Movable Modal"
			"2 - Modal Dialog"
			"3 - Floating Window"
			"4 - Plain Box"
			"5 - Shadowed Box"
			"6 - Rounded Window"
			"7 - Global Floating Window"
			"8 - Sheet Window"
			"9 - Metal Window"
			"11 - Modeless Dialog"
		#tag EndEnumValues
	#tag EndViewProperty
	#tag ViewProperty
		Name="FullScreen"
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="FullScreenButton"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackColor"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Position"
		InitialValue="400"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="ImplicitInstance"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Interfaces"
		Visible=true
		Group="ID"
		Type="String"
		EditorType="String"
	#tag EndViewProperty
	#tag ViewProperty
		Name="LiveResize"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MacProcID"
		Visible=true
		Group="Appearance"
		InitialValue="0"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MaxHeight"
		Visible=true
		Group="Position"
		InitialValue="32000"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MaximizeButton"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MaxWidth"
		Visible=true
		Group="Position"
		InitialValue="32000"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MenuBar"
		Visible=true
		Group="Appearance"
		Type="MenuBar"
		EditorType="MenuBar"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MenuBarVisible"
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinHeight"
		Visible=true
		Group="Position"
		InitialValue="64"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimizeButton"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinWidth"
		Visible=true
		Group="Position"
		InitialValue="64"
		Type="Integer"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		Type="String"
		EditorType="String"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Placement"
		Visible=true
		Group="Position"
		InitialValue="0"
		Type="Integer"
		EditorType="Enum"
		#tag EnumValues
			"0 - Default"
			"1 - Parent Window"
			"2 - Main Screen"
			"3 - Parent Window Screen"
			"4 - Stagger"
		#tag EndEnumValues
	#tag EndViewProperty
	#tag ViewProperty
		Name="Resizeable"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		Type="String"
		EditorType="String"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Title"
		Visible=true
		Group="Appearance"
		InitialValue="Untitled"
		Type="String"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType="Boolean"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Position"
		InitialValue="600"
		Type="Integer"
	#tag EndViewProperty
#tag EndViewBehavior
